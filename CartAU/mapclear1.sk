options:
    error: &4&lERROR &8| &7
    prefix: &7[&e&lCARTAU&7] &7|

# Command to manage map clearing
command /mapclear <text> [<text>]:
    permission: server.mapclear
    permission message: {@error}You do not have the required permission &8(&c&lserver.spawn&8)
    usage: {@error}Incorrect usage&8: &c&l/mapclear help
    trigger:
        if arg-1 is "forceclear":
            clearMap()
            broadcast "{@prefix} The map has been cleared by &c&l%player's displayname%&7!"
            broadcast "{@prefix}&c&lFall damage &7will be &c&ldisabled &7for the next &c&l30 seconds&7..."
        else if arg-1 is "setfrequency":
            if arg-2 is not set:
                message "{@error}Incorrect usage: &c&l/mapclear setfrequency <text>"
                stop
            
            set {_time} to arg-2 parsed as time period
            
            if "%{_time}%" is "<none>":
                message "{@error}The timespan you provided is not valid, valid examples include: 5m, 1d12h, 100s"
                stop
            
            set {server::mapclear::frequency} to {_time}
            message "{@prefix}Set map clear &c&lfrequency&7 to &c&l%{_time}%"
        else:
            message "&c&l/mapclear forceclear"
            message "&c&l/mapclear setfrequency <time>"

on place:
    if player's gamemode is survival:
        add location of event-block to {server::playerplacedblocks::*}

every 5 seconds:
    set {_time} to difference between now and {server::mapclear::lastclear}
    
    if {_time} >= {server::mapclear::frequency}:
        clearMap()
        broadcast "{@prefix}The map has &c&lcleared &7automatically"
        broadcast "{@prefix}&c&lFall damage &7will be &c&ldisabled &7for the next &c&l30 seconds"

on damage:
    if victim is a player:
        if damage cause is fall:
            set {_time} to difference between now and {server::mapclear::lastclear}
            if {_time} < 30 seconds:
                cancel event

function clearMap():
    loop {server::playerplacedblocks::*}:
        if block at loop-value is not any shulker box:
            set block at loop-value to air
    delete {server::playerplacedblocks::*}
    
    set {server::mapclear::lastclear} to now

# Update scoreboard variable every second
every 1 second:
    set {_lastClearTime} to difference between now and {server::mapclear::lastclear}
    set {_frequency} to {server::mapclear::frequency}
    if {_frequency} is set:
        set {_nextClearIn} to {_frequency} - {_lastClearTime}
        if {_nextClearIn} > 0:
            set {server::mapclear::nextclear} to {_nextClearIn}
        else:
            set {server::mapclear::nextclear} to 0
    else:
        set {server::mapclear::nextclear} to -1  # Indicates frequency not set

# You can later access {server::mapclear::nextclear} in your config or other scripts
